import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

allprojects {
    group 'org.citygml4j.tools'
    version '1.4.3'
    ext.citygml4jVersion = '2.11.3'
    ext.date = new Date()

    repositories {
        maven {
            url 'https://repo.osgeo.org/repository/release'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    dependencies {
        implementation "org.citygml4j:citygml4j:$citygml4jVersion"
    }

    plugins.withType(MavenPublishPlugin) {
        java {
            withJavadocJar()
            withSourcesJar()
        }

        jar {
            manifest {
                attributes('Implementation-Title': project.name,
                        'Implementation-Version': project.version
                )
            }
            into('META-INF') {
                from "$rootDir/LICENSE"
            }
        }

        javadoc {
            options {
                overview "$buildDir/tmp/javadoc/overview.html"
                header "$project.name $project.version"
                bottom """
                    <a href="https://github.com/citygml4j/citygml-tools/issues">Report a bug or suggest an enhancement</a><br>
                    ${project.name} is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br>
                    Official website and more information at: <a target="_blank" href="https://github.com/citygml4j/citygml-tools">https://github.com/citygml4j/citygml-tools</a><br>
                    Copyright &copy; 2018-${date.format('yyyy')}<br>
                    Claus Nagel &lt;claus.nagel@gmail.com&gt;
                    """.replaceAll("[\r|\n]+", "")
                addStringOption('doctitle', project.name + ' - ' + project.version)
            }

            if (JavaVersion.current().isJava9Compatible()) {
                options.addBooleanOption('html5', true)
            }

            doFirst {
                copy {
                    from "$rootDir/resources/javadoc/overview.html"
                    into "$buildDir/tmp/javadoc"
                    filter(ReplaceTokens, tokens: [
                            name: project.name,
                            version: project.version,
                            citygmlTools: rootProject.name
                    ])
                }
            }
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    pom {
                        name = project.name
                        description = project.description
                        url = 'https://github.com/citygml4j/citygml-tools'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                id = 'clausnagel'
                                name = 'Claus Nagel'
                                email = 'claus.nagel@gmail.com'
                                roles = ['architect', 'developer']
                                timezone = '+1'
                            }
                        }
                        scm {
                            url = 'https://github.com/citygml4j/citygml-tools'
                            connection = 'scm:git:https://github.com/citygml4j/citygml-tools.git'
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }
    }
}

dependencies {
    implementation project(':citygml-tools-common')
    implementation project(':global-app-mover')
    implementation project(':height-changer')
    implementation project(':lod-filter')
    implementation project(':reproject')
    implementation project(':texture-clipper')
    implementation "org.citygml4j:citygml4j:$citygml4jVersion"
    implementation 'info.picocli:picocli:4.6.1'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

application {
    mainClass = 'org.citygml4j.tools.CityGMLTools'
    applicationDefaultJvmArgs = ['-Xms1G']
    executableDir = ''

    startScripts {
        doLast {
            def windowsScript = getWindowsScript()
            def unixScript = getUnixScript()
            windowsScript.text = windowsScript.text.replaceFirst('set APP_HOME=.*', '$0\r\nset WORKING_DIR=%CD%')
            unixScript.text = unixScript.text.replaceFirst('APP_HOME=.*', '$0\nexport APP_HOME\nexport WORKING_DIR=\\$SAVED')
        }
    }
}

distributions.main.contents {
    from('resources/readme/README') {
        filter(ReplaceTokens, tokens: [
                name: project.name,
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: java.sourceCompatibility.toString()
        ])
    }
    into('license') {
        from 'LICENSE'
    }
    from(file("$buildDir/tmp/dir")) {
        mkdir "$buildDir/tmp/dir/ade-extensions"
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}