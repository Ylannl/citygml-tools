import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'distribution'
}

version '1.2.0'
mainClassName = 'org.citygml4j.tools.CityGMLTools'

sourceCompatibility = 1.8

ext {
    citygml4jVersion = '2.9.2-b190218.1904'
    date = new Date()
}

allprojects {
    group 'org.citygml4j.tools'

    repositories {
        maven {
            url 'https://repository.apache.org/content/repositories/snapshots'
        }
        jcenter()
    }
}

subprojects {
    apply plugin: 'java-library'

    sourceCompatibility = 1.8

    dependencies {
        implementation "org.citygml4j:citygml4j:$citygml4jVersion"
    }

    plugins.withType(MavenPublishPlugin) {
        jar {
            manifest {
                attributes('Implementation-Title': project.name,
                        'Implementation-Version': project.version
                )
            }
            into('META-INF') {
                from "$rootDir/LICENSE"
            }
        }

        javadoc {
            options {
                overview "$buildDir/tmp/javadoc/overview.html"
                header "$project.name $project.version"
                bottom """
                    <a href="https://github.com/citygml4j/citygml-tools/issues">Report a bug or suggest an enhancement</a><br>
                    ${project.name} is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br>
                    Official website and more information at: <a target="_blank" href="https://github.com/citygml4j/citygml-tools">https://github.com/citygml4j/citygml-tools</a><br>
                    Copyright &copy; 2018-${date.format('yyyy')}<br>
                    Claus Nagel &lt;claus.nagel@gmail.com&gt;
                    """.replaceAll("[\r|\n]+", "")
                addStringOption('doctitle', project.name + ' - ' + project.version)
                addStringOption('Xdoclint:none', '-quiet')
            }

            doFirst {
                copy {
                    from "$rootDir/resources/javadoc/overview.html"
                    into "$buildDir/tmp/javadoc"
                    filter(ReplaceTokens, tokens: [
                            name: project.name,
                            version: project.version,
                            citygmlTools: rootProject.name,
                            copyright: date.format('yyyy')
                    ])
                }
            }
        }

        task sourcesJar(type: Jar, dependsOn: classes) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            archiveClassifier = 'javadoc'
            from javadoc.destinationDir
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                    pom {
                        name = project.name
                        description = project.description
                        url = 'https://github.com/citygml4j/citygml-tools'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                id = 'clausnagel'
                                name = 'Claus Nagel'
                                email = 'claus.nagel@gmail.com'
                                roles = ['architect', 'developer']
                                timezone = '+1'
                            }
                        }
                        scm {
                            url = 'https://github.com/citygml4j/citygml-tools'
                            connection = 'scm:git:https://github.com/citygml4j/citygml-tools.git'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation project(':citygml-tools-common')
    implementation project(':global-app-mover')
    implementation project(':height-changer')
    implementation project(':texture-clipper')
    implementation "org.citygml4j:citygml4j:$citygml4jVersion"
    implementation 'info.picocli:picocli:3.9.3'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

startScripts {
    defaultJvmOpts = ['-Xms1G']
    doLast {
        def windowsScript = getWindowsScript()
        def unixScript = getUnixScript()
        windowsScript.text = windowsScript.text.replaceFirst('set APP_HOME=.*', '$0\r\nset WORKING_DIR=%CD%')
        unixScript.text = unixScript.text.replaceFirst('APP_HOME=.*', '$0\nexport APP_HOME\nexport WORKING_DIR=\\$SAVED')
    }
}

distributions.main.contents {
    from('resources/doc/README') {
        filter(ReplaceTokens, tokens: [
                name: project.name,
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: sourceCompatibility.toString()
        ])
    }
    into('license') {
        from 'LICENSE'
    }
    from(file("$buildDir/tmp/dir")) {
        mkdir "$buildDir/tmp/dir/ade-extensions"
    }
}